/*
 * Copyright (c) 2025, WSO2 LLC. (http://www.wso2.com).
 *
 * WSO2 LLC. licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

plugins {
    id 'base'
    id 'net.researchgate.release'
}

allprojects {
    group = project.group
    version = project.version

    apply plugin: 'maven-publish'

    ext {
        timestampedVersionRegex = '.*-\\d{8}-\\d{6}-\\w{8}.*'
        snapshotVersion = '-SNAPSHOT'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url = 'https://maven.wso2.org/nexus/content/repositories/releases/'
        }

        maven {
            url = 'https://maven.wso2.org/nexus/content/groups/wso2-public/'
        }

        maven {
            url = 'https://repo.maven.apache.org/maven2'
        }

        maven {
            url = 'https://maven.pkg.github.com/ballerina-platform/*'
            credentials {
                username System.getenv("packageUser")
                password System.getenv("packagePAT")
            }
        }
    }
}

def moduleVersion = project.version.replace("-SNAPSHOT", "")

task buildNative {
    dependsOn ':native:shadowJar'
    doLast {
        copy {
            from "${project.projectDir}/native/build/libs/native-${project.version}-all.jar"
            into "${project.projectDir}/ballerina/lib"
            rename { "aws-s3-native-${moduleVersion}.jar" }
        }
    }
}

task buildBallerina(type: Exec) {
    dependsOn buildNative
    workingDir 'ballerina'
    commandLine 'bal', 'build'
}

task testBallerina(type: Exec) {
    dependsOn buildNative
    workingDir 'ballerina'
    commandLine 'bal', 'test'
}

task cleanAll {
    delete 'ballerina/target'
    delete 'ballerina/lib'
    dependsOn ':native:clean'
}

clean {
    delete 'ballerina/target'
    delete 'ballerina/lib'
}

release {
    buildTasks = ['buildBallerina']
    failOnSnapshotDependencies = true
    versionPropertyFile = 'gradle.properties'
    tagTemplate = 'v${version}'
    git {
        requireBranch = "release-${moduleVersion}"
        pushToRemote = 'origin'
    }
}

defaultTasks 'buildBallerina'
